buildscript {
	repositories {
		jcenter()

		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.1.2"
group = "com.five35.minecraft.ice"
archivesBaseName = "Ice"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile files("../Baubles-1.8.9-1.1.3.0-deobf.jar")
}

minecraft {
	mappings = "stable_20"
	runDir = "run"
	version = "1.8.9-11.15.1.1722"
}

processResources {
	def substitutionTargets = [
		"mcmod.info",
		"version.properties",
	]

	def substitutions = [
		modid: project.archivesBaseName,
		version: project.version,
		mcversion: project.minecraft.version,
	]

	inputs.properties substitutions

	from(sourceSets.main.resources.srcDirs) {
		include substitutionTargets
		expand substitutions
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude substitutionTargets
	}
}

task apiZip(type: Zip) {
	classifier = "api"

	from(sourceSets.main.java) {
		include "com/five35/minecraft/ice/api/**"
	}
}

task deobfJar(type: Jar) {
	classifier = "deobf"
	from sourceSets.main.output
}

artifacts {
	archives apiZip
	archives deobfJar
}
